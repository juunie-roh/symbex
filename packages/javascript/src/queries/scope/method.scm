;; methods
;; @decorator @is_static @is_async @name@params @body
(method_definition
  decorator: (decorator)* @decorator
  "static"? @is_static
  "async"? @is_async
  name: (_) @name
  parameters: (formal_parameters) @params
  body: (statement_block) @body
  (#not-match? @name "^(constructor)$")
) @node

;; arrow function / function expression methods
(field_definition
  decorator: (decorator)* @decorator
  "static"? @is_static
  property: (_) @name
  value: [
    ;; @decorator @is_static @name = @is_async @params => @body
    (arrow_function
      "async"? @is_async
      parameters: [(formal_parameters) (identifier)] @params
      body: (_) @body)
    ;; @decorator @is_static @name = @is_async function @params @body
    (function_expression
      "async"? @is_async
      parameters: (formal_parameters) @params
      body: (statement_block) @body)
  ]
) @node